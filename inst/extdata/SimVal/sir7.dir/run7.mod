;; 1. Based on: 6
;; 2. Description:
;;    Final scm model
;; 3. Label:
;;    SimVal base model
;------------------------------------------------------------------------------
$PROBLEM    run 1
$INPUT      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE
            FOOD FORM TYPE WT HT AGE SEX RACE ETHNIC GENO SMOK AST ALT
            BILI BMI CRCL NCI NCIL RACEL RACEL1 RACEL2 RACEL3 GENO1
            GENO2 GENO3 GENO4
$DATA      ../../../ProducedData/Dataset/DAT-1-MI-PMX-2.csv IGNORE=@
            IGNORE(TYPE.EQN.2) IGNORE(BLQ.EQN.1) IGNORE=(ID.EQN.895)
$SUBROUTINE ADVAN2 TRANS2
$PK
;;; FRELSEX-DEFINITION START
IF(SEX.EQ.1.0000E+00) FRELSEX = 1  ; Most common
IF(SEX.EQ.2.0000E+00) FRELSEX = ( 1 + THETA(14))
;;; FRELSEX-DEFINITION END


;;; FRELGENO4-DEFINITION START
IF(GENO4.EQ.0.0000E+00) FRELGENO4 = 1  ; Most common
IF(GENO4.EQ.1.0000E+00) FRELGENO4 = ( 1 + THETA(13))
;;; FRELGENO4-DEFINITION END


;;; FRELFORM-DEFINITION START
IF(FORM.EQ.1.0000E+00) FRELFORM = 1  ; Most common
IF(FORM.EQ.0.0000E+00) FRELFORM = ( 1 + THETA(12))
;;; FRELFORM-DEFINITION END

;;; FREL-RELATION START
FRELCOV=FRELFORM*FRELGENO4*FRELSEX
;;; FREL-RELATION END


;;; CLFOOD-DEFINITION START
IF(FOOD.EQ.1.0000E+00) CLFOOD = 1  ; Most common
IF(FOOD.EQ.0.0000E+00) CLFOOD = ( 1 + THETA(11))
;;; CLFOOD-DEFINITION END

;;; CL-RELATION START
CLCOV=CLFOOD
;;; CL-RELATION END


TVFREL  = THETA(1)

TVFREL = FRELCOV*TVFREL

CLWT    =  (WT/75)**THETA(2)
CLGENO1 = 1
CLGENO2 = 1
CLGENO3 = 1
CLGENO4 = 1
IF(GENO1.EQ.1) CLGENO1 = (1+THETA(8))
IF(GENO3.EQ.1) CLGENO3 = (1+THETA(9))
IF(GENO4.EQ.1) CLGENO4 = (1+THETA(10))

CLCOV1 = CLWT*CLGENO1*CLGENO2*CLGENO3*CLGENO4

VWT   = (WT/75)**THETA(3)
VCOV1  = VWT

TVCL    = THETA(4) * CLCOV1

TVCL = CLCOV*TVCL
TVV     = THETA(5) * VCOV1
TVMAT   = THETA(6)
TVD1    = THETA(7)

FREL  = TVFREL  * EXP(ETA(2))
CL    = TVCL    * EXP(ETA(3))
V     = TVV     * EXP(ETA(4))
MAT   = TVMAT   * EXP(ETA(5))
D1    = MAT*(1-TVD1)

F1    = FREL
KA    = 1 / (MAT-D1)
S2    = V

$ERROR
CP    = A(2)*1000 / V
IPRED = LOG(CP + 0.00001)
Y     = IPRED + EPS(1) * EXP(ETA(1))

$THETA  1 FIX ; 1. TVFREL
$THETA  0.75 FIX ; 2. WT coef on TVCL
$THETA  1 FIX ; 3. WT coef on TVV
$THETA  (0,11.9) ; 4. TVCL
$THETA  (0,191) ; 5. TVV
$THETA  (0,6.47) ; 6. TVMAT
$THETA  (0,0.616,1) ; 7 TVD1
$THETA  (-1,1.24) ; GENO2=1 on CL
$THETA  (-1,-0.335) ; GENO3=1 on CL
$THETA  (-1,-0.515) ; GENO4=1 on CL
$THETA  (-1.00,0.252,3.00) ; CLFOOD1
$THETA  (-1.00,-0.145,3.00) ; FRELFORM1
$THETA  (-1.00,2.64,3.00) ; FRELGENO41
$THETA  (-1.00,-0.139,3.00) ; FRELSEX1
$OMEGA  0.0257  ; 1. IIV on RUV
$OMEGA  0.256  ; 2. IIV on FREL
$OMEGA  0.0476  ; 3. IIV on CL
$OMEGA  0.159  ; 4. IIV on V
$OMEGA  0.144  ; 5. IIV on MAT
$SIGMA  0.0444  ;     1. RUV
$ESTIMATION METHOD=1 INTER ETASTYPE=1 MAXEVAL=9999 PRINT=1 NOABORT
$COVARIANCE PRINT=E MATRIX=S UNCONDITIONAL
$TABLE      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE
            FOOD FORM TYPE WT HT AGE SEX RACE ETHNIC GENO SMOK AST ALT
            BILI BMI CRCL NCI NCIL RACEL RACEL1 RACEL2 RACEL3 GENO1
            GENO2 GENO3 GENO4 CPRED CIPREDI CWRES CIWRES ETAS(1:LAST)
            FREL CL V MAT TVD1 NOPRINT ONEHEADER FILE=xptab7

