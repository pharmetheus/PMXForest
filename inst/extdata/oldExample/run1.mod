;; 1. Based on: ../../Simulations/ModeFRX/run4-1scmplus.dir/finalModel
;; 2. Description:
;;    MATRIX=S
;; 3. Label:
;;    DAT-1, TYPE=1
;------------------------------------------------------------------------------
$PROBLEM    run 1

$INPUT      NO ID STUDYID TAD TIME DAY AMT RATE ODV DV EVID BLQ DOSE FOOD 
            FORM TYPE WT HT AGE SEX RACE ETHNIC GENO 
            SMOK AST ALT BILI BMI CRCL NCI NCIL 
            RACEL RACEL1 RACEL2 RACEL3 GENO1 GENO2 
            GENO3 GENO4

$DATA      DAT-1-MI-PMX-2.csv
            IGNORE=@ IGNORE(TYPE.EQN.2)
            IGNORE(BLQ.EQN.1)
$SUBROUTINE ADVAN2 TRANS2
$PK
;;; FRELNCIL-DEFINITION START
IF(NCIL.EQ.0.0000E+00) FRELNCIL = 1  ; Most common
IF(NCIL.EQ.1.0000E+00) FRELNCIL = ( 1 + THETA(16))
;;; FRELNCIL-DEFINITION END


;;; FRELFORM-DEFINITION START
IF(FORM.EQ.1.0000E+00) FRELFORM = 1  ; Most common
IF(FORM.EQ.0.0000E+00) FRELFORM = ( 1 + THETA(15))
;;; FRELFORM-DEFINITION END

;;; FREL-RELATION START
FRELCOV=FRELFORM*FRELNCIL
;;; FREL-RELATION END


;;; CLFOOD-DEFINITION START
IF(FOOD.EQ.1.0000E+00) CLFOOD = 1  ; Most common
IF(FOOD.EQ.0.0000E+00) CLFOOD = ( 1 + THETA(14))
;;; CLFOOD-DEFINITION END

;;; CL-RELATION START
CLCOV=CLFOOD
;;; CL-RELATION END



TVFREL  = THETA(1)
IF(GENO.EQ.1) TVFREL = TVFREL*(1+THETA(11))
IF(GENO.EQ.3) TVFREL = TVFREL*(1+THETA(12))
IF(GENO.EQ.4) TVFREL = TVFREL*(1+THETA(13))

TVFREL = FRELCOV*TVFREL

TVCL    = THETA(4) * (WT/75)**THETA(2)

IF(GENO.EQ.1) TVCL = TVCL*(1+THETA(8))
IF(GENO.EQ.3) TVCL = TVCL*(1+THETA(9))
IF(GENO.EQ.4) TVCL = TVCL*(1+THETA(10))

TVCL = CLCOV*TVCL

TVV     = THETA(5) * (WT/75)**THETA(3)
TVMAT   = THETA(6)
TVD1    = THETA(7)

FREL  = TVFREL  * EXP(ETA(2))
CL    = TVCL    * EXP(ETA(3))
V     = TVV     * EXP(ETA(4))
MAT   = TVMAT   * EXP(ETA(5))
D1    = MAT*(1-TVD1)

F1    = FREL
KA    = 1 / (MAT-D1)
S2    = V

$ERROR
CP    = A(2)*1000 / V
IPRED = LOG(CP + 0.00001)
Y     = IPRED + EPS(1) * EXP(ETA(1))

$THETA  1 FIX ; 1. TVFREL
$THETA  0.75 FIX ; 2. WT coef on TVCL
$THETA  1 FIX ; 3. WT coef on TVV
$THETA  (0,13.3) ; 4. TVCL
$THETA  (0,216) ; 5. TVV
$THETA  (0,6.43) ; 6. TVMAT
$THETA  (0,0.618,1) ; 7 TVD1
$THETA  (-1,0.737) ; GENO=1 on CL
$THETA  (-1,-0.235) ; GENO=3 on CL
$THETA  (-1,-0.51) ; GENO=4 on CL
$THETA  (-1,-0.424) ; GENO=1 on CL_
$THETA  (-1,0.309) ; GENO=3 on CL_
$THETA  (-1,2.8) ; GENO=4 on CL_
$THETA  (-1.00,0.253,3.00) ; CLFOOD1
$THETA  (-1.00,-0.144,3.00) ; FRELFORM1
$THETA  (-1.00,0.255,3.00) ; FRELNCIL1
$OMEGA  0.0259  ; 1. IIV on RUV
$OMEGA  0.251  ; 2. IIV on FREL
$OMEGA  0.0475  ; 3. IIV on CL
$OMEGA  0.154  ; 4. IIV on V
$OMEGA  0.144  ; 5. IIV on MAT
$SIGMA  0.0444  ;     1. RUV
$ESTIMATION METHOD=1 INTER ETASTYPE=1 MAXEVAL=9999 PRINT=1 NOABORT
$COVARIANCE PRINT=E MATRIX=S UNCONDITIONAL
$TABLE      NO ID STUDYID TAD TIME AMT RATE ODV DV EVID BLQ DOSE FOOD
            FORM TYPE WT HT AGE SEX RACE ETHNIC GENO SMOK AST ALT BILI BMI
            CRCL NCI NCIL RACEL RACEL2 RACEL3 GENO2 GENO3 GENO4 CPRED
            CIPREDI CWRES CIWRES ETAS(1:LAST) NOPRINT ONEHEADER
            FILE=xptab1
